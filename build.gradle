apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'

project.group = 'de.hhu.stups'
project.version = '2.0.2-SNAPSHOT'
project.archivesBaseName = "prob2-ui-servlets"

sourceCompatibility = 1.6
def gradle_version = '1.0-rc-3'


repositories {
	mavenCentral()

	maven {
		name "cobra"
		url "http://cobra.cs.uni-duesseldorf.de/artifactory/repo"
	}
}


configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }


sourceSets.main.java.srcDirs = [
	'src/main/generated'] // ie there's no stand-alone java source
sourceSets.main.groovy.srcDir 'src/main/java' // ie include the 'java' dir as groovy source

def kernel_version = "2.0.0-SNAPSHOT"

dependencies {
  compile group: "de.prob2", name: "de.prob2.kernel", version: kernel_version

  compile 'commons-cli:commons-cli:1.2' // Apache Licence 2.0

	// Used for escaping HTML and Unicode symbols within Java strings
	compile 'commons-lang:commons-lang:2.6'

	// dependency for Worksheets
	compile 'org.pegdown:pegdown:1.1.0' // Apache Licence 2.0    Used for Worksheet

	compile 'com.google.inject:guice:3.0' // Apache Licence 2.0
	compile 'com.google.inject.extensions:guice-servlet:3.0' // Apache Licence 2.0
	compile 'org.slf4j:slf4j-api:1.6.1' // MIT
	compile 'ch.qos.logback:logback-core:0.9.29' // EPL
	compile 'ch.qos.logback:logback-classic:0.9.29' // EPL

	compile 'com.google.code.gson:gson:1.7.1' // Apache Licence 2.0

	// If you consider to update or downgrade jetty ... just don't
	// it'll make you puke
	compile 'org.eclipse.jetty:jetty-io:8.1.8.v20121106' // EPL
	compile 'org.eclipse.jetty:jetty-server:8.1.8.v20121106' // EPL
	compile 'org.eclipse.jetty:jetty-servlet:8.1.8.v20121106' // EPL
	compile 'org.eclipse.jetty:jetty-util:8.1.8.v20121106' // EPL
	compile 'org.eclipse.jetty:jetty-webapp:8.1.8.v20121106' // EPL

	compile 'com.github.spullara.mustache.java:compiler:0.8.12' // Apache Licence 2.0

}

task deploy(type: Copy, dependsOn: build) {
    into "$buildDir/libs"
    from configurations.runtime
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

apply plugin: 'signing'

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
	 classifier = 'javadoc'
	 from javadoc
}

task sourcesJar(type: Jar) {
	 classifier = 'sources'
	 from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'ProB 2.0 Servlets'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Servlets for ProB 2.0. This will be replaced by a new mechanism.'
        url 'https://github.com/bendisposto/prob2-ui-servlets'

        licenses {
          license {
            name 'Eclipse Public License, Version 1.0'
            url 'https://www.eclipse.org/legal/epl-v10.html'
          }
        }

        scm {
          connection 'scm:git:git://github.com/bendisposto/prob2-ui-servlets.git'
          developerConnection 'scm:git:git@github.com:bendisposto/prob2-ui-servlets.git'
          url 'https://github.com/bendisposto/prob2-ui-servlets'
        }


        developers {
          developer {
            id 'bendisposto'
            name 'Jens Bendisposto'
            email 'jens@bendisposto.de'
          }
        }
      }
    }
  }
}
}
